#!/usr/bin/env perl
use strict;
use warnings;
use File::Basename qw(dirname basename fileparse);
use File::Path qw(make_path);
use File::Spec::Functions qw(catdir rel2abs abs2rel canonpath);
use String::ShellQuote qw(shell_quote);
use Cwd qw(abs_path getcwd);

my $appdir=catdir($ENV{HOME},'src','Fiji.app');
my $arch = qx(uname -m);
chomp $arch;
my $platform = $arch eq 'x86_64'? 'linux64' : $arch eq 'i686'? 'linux32' : 'linux';
$arch = 'i386' if $arch eq 'i686';

# get hte list of libraries installed by ubuntu-desktop installer
my $syslibs = "$platform.syslibs";
#if (!-e $syslibs) {
#  system(q(wget -q -O- http://cdimage.ubuntu.com/ubuntu-server/trusty/daily/current/trusty-server-amd64.manifest |perl -nle '/^(\\S+)/ && print $1' |xargs -n 1 dpkg -L 2>/dev/null |perl -nle 'm{^/lib/|^/usr/lib/} && m{/lib([^/]+).so[^/]*} && print $1' |sort |uniq | tee ).shell_quote($syslibs));
#  die "Error running syslibs command: ".($?>>8) if $?>>8;
#}
my %syslibs;
if (-e $syslibs) {
  %syslibs = map {chomp; ($_=>1)} do {local @ARGV = $syslibs; <>};
}
$syslibs{'linux-vdso'} = 1;
$syslibs{"ld-linux-i686"} = 1;
$syslibs{"ld-linux-x86-64"} = 1;
$syslibs{"c"} = 1;

my $libdir=catdir($appdir, 'mm', $platform);
my %gphotodir=(
  libgphoto2=>abs_path([glob("/usr/lib/$arch-linux-gnu/libgphoto2/*")]->[0]),
  libgphoto2_port=>abs_path([glob("/usr/lib/$arch-linux-gnu/libgphoto2_port/*")]->[0]),
);
my %seen;

make_path(catdir($libdir), {error=>my $err});
die $err if $err;
print join(' ','mkdir','-pv',shell_quote($libdir))."\n";
opendir my $d, $appdir or die;
for my $f (readdir($d)) {
  my $file = catdir($appdir, $f);
  if (-f $file && -x $file && -B $file && basename($file) !~ /^ImageJ-|^fiji-/i) {
    my $destfile = catdir($libdir,basename($file));
    print join(' ','mv','-v',shell_quote($file),shell_quote($destfile))."\n";
    findlib($file, $destfile, $libdir);
  }
}
closedir $d;

for my $dir (sort keys %gphotodir) {
  my $gphotodir = $gphotodir{$dir};
  make_path(catdir($libdir,'libgphoto2',$dir), {error=>my $err});
  die $err if $err;
  print join(' ','mkdir','-pv',shell_quote(catdir($libdir,'libgphoto2',$dir)))."\n";

  opendir my $d, $gphotodir or die;
  for my $f (readdir($d)) {
    my $file = catdir($gphotodir, $f);
    if ($file =~ /\.so$/ && -f $file) {
      my $destfile = catdir($libdir,'libgphoto2',$dir,basename($file));
      print join(' ','install','-v','-m','0755',shell_quote($file),shell_quote($destfile))."\n";
      findlib($file, $destfile, $libdir);
    }
  }
  closedir $d;
}

exit 0;

sub findlib {
  my ($src, $dest, $libdest) = @_;
  my $rellibdest = abs2rel($libdest, dirname($dest));
  print join(' ','patchelf','--set-rpath',shell_quote(catdir('$ORIGIN', $rellibdest)), shell_quote($dest))."\n";

  my $ldd = join(' ','ldd',shell_quote($src));
  my %libs = map {@$_} 
    grep {$_->[0]=~/^\// && basename($_->[0])=~/^(?:lib)?([^.\/]+)\.so/ && !$syslibs{$1}} 
    map {/^\s+(\S+)\s*=>\s*(\S+)/? [$2,$1] : /\s+(\S+)/? [$1,$1] : ()} 
    split /\n/, qx($ldd);
  for my $lib (sort keys %libs) {
    my $destlibname = $libs{$lib};
    (my $abslib = $lib) =~ s(^\$ORIGIN/|^\$\{ORIGIN\}/)();
    $abslib = abs_path(rel2abs($abslib, dirname($src)));
    my $abslibdest = abs_path(rel2abs($libdest, dirname($dest)));

    my $destfile = canonpath(catdir($abslibdest,basename($destlibname)));
    if (!$seen{basename($destfile)}) {
      print join(' ','install','-v','-m','0755',shell_quote($abslib),shell_quote($destfile))."\n";
      $seen{basename($destfile)} = $destfile;
      findlib($abslib, $destfile, $libdest);
    }
  }
}

